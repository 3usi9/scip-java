plugins {
    id 'java'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
}

repositories {
    jcenter()
    mavenLocal()
    maven { url = 'https://repo.maven.apache.org/maven2' }
    maven { url = 'https://repo.gradle.org/gradle/libs-releases' }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'commons-cli:commons-cli:1.4'
    implementation 'org.apache.commons:commons-lang3:3.11'
    implementation 'commons-io:commons-io:2.8.0'
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'org.slf4j:slf4j-nop:1.7.30'
    implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.9.0'
    implementation 'com.google.guava:guava:29.0-jre'
    implementation "org.gradle:gradle-tooling-api:6.6.1"

    testImplementation 'junit:junit:4.13'
}

application {
    mainClassName = 'lsifjava.MainKt'
}

jar {
    manifest {
        attributes 'Main-Class': application.mainClassName
    }
}

task fatJar(type: Jar) {
    manifest.from jar.manifest
    from {
        configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
        exclude "about.html"
    }
    with jar
}

group = 'com.sourcegraph'
version = '1.0'

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xinline-classes"]
    }
}